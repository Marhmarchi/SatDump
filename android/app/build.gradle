apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

android {
    compileSdkVersion 28
    buildToolsVersion "30.0.3"
    ndkVersion "25.1.8937393"
    defaultConfig {
        applicationId "com.altillimity.satdump"
        minSdkVersion 28
        targetSdkVersion 28
        versionCode 1
        versionName "1.1.0"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt')
        }
    }

    externalNativeBuild {
        cmake {
            version "3.18.1"
            path "../CMakeLists.txt"
        }
    }

    sourceSets {
        main {
            assets.srcDirs += ['assets']
        }
    }
}

task deleteTempAssets (type: Delete) {
    delete 'assets'
}

task copyResources(type: Copy) {
    description = 'Copy resources...'
    from '../../resources'
    into 'assets/resources'
    include('**/*')
}

task copyPipelines(type: Copy) {
    description = 'Copy pipelines...'
    from '../../pipelines'
    into 'assets/pipelines'
    include('**/*')
}

task copyCfg(type: Copy) {
    description = 'Copy config...'
    from '../../satdump_cfg.json'
    into 'assets/'
    include('*')
}

task copyPlugins_arm64v8a(type: Copy) {
    description = 'Copy Plugins (arm64-v8a)...'
    from '.cxx/cmake/release/arm64-v8a/plugins'
    into 'assets/plugins/arm64-v8a'
    include('*.so')
}

task copyPlugins_armeabiv7a(type: Copy) {
    description = 'Copy Plugins (armeabi-v7a)...'
    from '.cxx/cmake/release/armeabi-v7a/plugins'
    into 'assets/plugins/armeabi-v7a'
    include('*.so')
}

task copyPlugins_x86(type: Copy) {
    description = 'Copy Plugins (x86)...'
    from '.cxx/cmake/release/x86/plugins'
    into 'assets/plugins/x86'
    include('*.so')
}

task copyPlugins_x86_64(type: Copy) {
    description = 'Copy Plugins (x86_64)...'
    from '.cxx/cmake/release/x86_64/plugins'
    into 'assets/plugins/x86_64'
    include('*.so')
}

repositories {
    mavenCentral()
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.2'
    implementation 'androidx.appcompat:appcompat:1.0.2'
}

copyResources.dependsOn deleteTempAssets
copyPipelines.dependsOn deleteTempAssets
copyCfg.dependsOn deleteTempAssets

copyPlugins_arm64v8a.dependsOn deleteTempAssets
copyPlugins_armeabiv7a.dependsOn deleteTempAssets
copyPlugins_x86.dependsOn deleteTempAssets
copyPlugins_x86_64.dependsOn deleteTempAssets

preBuild.dependsOn copyResources
preBuild.dependsOn copyPipelines
preBuild.dependsOn copyCfg

preBuild.dependsOn copyPlugins_arm64v8a
preBuild.dependsOn copyPlugins_armeabiv7a
preBuild.dependsOn copyPlugins_x86
preBuild.dependsOn copyPlugins_x86_64